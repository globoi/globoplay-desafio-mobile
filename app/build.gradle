plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.nunkison.globoplaymobilechallenge'
    compileSdk 33

    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    defaultConfig {
        applicationId "com.nunkison.globoplaymobilechallenge"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            resValue("string", "api_key", localProperties['api.key'])
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("string", "api_key", localProperties['api.key'])
        }
    }
    flavorDimensions "version"
    productFlavors {
        mobile {
            dimension "version"
            minSdk 24

        }
        tv {
            dimension "version"
            minSdk 23
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    def retrofit_version = '2.9.0'
    def exoplayer_version = '2.19.0'
    def mockk_version = '1.13.5'
    def koin_android_version = '3.4.2'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-dash:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform('androidx.compose:compose-bom:2022.10.00')

    mobileImplementation 'androidx.compose.ui:ui'
    mobileImplementation 'androidx.compose.ui:ui-graphics'
    mobileImplementation 'androidx.compose.ui:ui-tooling-preview'
    mobileImplementation 'androidx.compose.material3:material3'
    mobileImplementation 'androidx.navigation:navigation-compose:2.6.0'
    mobileImplementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'
    mobileImplementation 'androidx.compose.runtime:runtime-livedata:1.4.3'
    mobileImplementation "io.insert-koin:koin-androidx-compose:$koin_android_version"
    implementation 'io.coil-kt:coil-compose:2.4.0'

    tvImplementation 'androidx.leanback:leanback:1.0.0'
    tvImplementation 'com.github.bumptech.glide:glide:4.15.1'
    tvImplementation 'jp.wasabeef:glide-transformations:4.3.0'
    tvImplementation 'androidx.media3:media3-exoplayer-ima:1.1.0'
    tvImplementation 'androidx.leanback:leanback-tab:1.1.0-beta01'
    tvImplementation "io.insert-koin:koin-android:$koin_android_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk-android:$mockk_version"
    testImplementation "io.mockk:mockk-agent:$mockk_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
    androidTestImplementation "io.mockk:mockk-agent:$mockk_version"


}